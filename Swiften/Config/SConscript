import os.path

Import("env")

def replaceSwiftenPath(input) :
	return input.replace(env.Dir("#").abspath, "#")

def cStringVariable(env, cVar, sconsVar) :
	result = "const char* " + cVar + "[] = {\n"
	# FIXME: Probably not very robust
	for var in sconsVar.split(" ") :
		result += "\t\"" + env.subst(var).replace("\\", "\\\\") + "\",\n"
	result += "\t0\n"
	result += "};\n"
	return result

config_flags = ""

swiften_env = env.Clone()
swiften_env.MergeFlags(swiften_env["SWIFTEN_FLAGS"])
swiften_env.MergeFlags(swiften_env["SWIFTEN_DEP_FLAGS"])

cppflags = replaceSwiftenPath(" ".join([swiften_env.subst("$_CPPDEFFLAGS"), swiften_env.subst("$_CPPINCFLAGS")]))
config_flags += cStringVariable(swiften_env, "CPPFLAGS", cppflags)

libflags = replaceSwiftenPath(" ".join([swiften_env.subst("$_LIBDIRFLAGS"), swiften_env.subst("$_LIBFLAGS")]))
config_flags += cStringVariable(swiften_env, "LIBFLAGS", libflags)

config_env = env.Clone()
# Create a local copy of Paths.cpp to avoid a Swiften dependency
config_env.Install(".", "#/Swiften/Base/Paths.cpp")
config_env.MergeFlags(config_env["BOOST_FLAGS"])
config_env.MergeFlags(config_env["PLATFORM_FLAGS"])
config_env.WriteVal("swiften-config.h", config_env.Value(config_flags))
swiften_config = config_env.Program("swiften-config", [
		"Paths.cpp",
		"swiften-config.cpp"
	])

if swiften_env.get("SWIFTEN_INSTALLDIR", "") :
	swiften_env.Install(os.path.join(swiften_env["SWIFTEN_INSTALLDIR"], "bin"), swiften_config)
