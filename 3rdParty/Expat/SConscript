Import(["env", "conf_env"])

if env.get("EXPAT_BUNDLED", False) :
	if env["SCONS_STAGE"] == "flags" :
		env["EXPAT_FLAGS"] = {
				"CPPDEFINES": ["XML_STATIC"],
				"CPPPATH": [Dir("src")],
				"LIBPATH": [Dir(".")],
				"LIBS": ["Swiften_Expat"],
			}

	if env["SCONS_STAGE"] == "build" :
		myenv = env.Clone()
		myenv.Append(CPPDEFINES = ["XML_STATIC", "HAVE_EXPAT_CONFIG_H"])
		myenv.Append(CPPPATH = [".", "src"])

		conf = Configure(conf_env)
		if conf.CheckFunc('memmove') :
			myenv.Append(CPPDEFINES = ["HAVE_MEMMOVE"])
		conf.Finish()

		myenv.StaticLibrary("Swiften_Expat", [
				"src/xmltok.c",
				"src/xmlparse.c",
				"src/xmlrole.c"
			])
